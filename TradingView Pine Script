//@version=5
indicator("Slope Convexity Indicator", overlay=false)


adjust_parameter() =>
    daily_hours = 6.5
    current_tf_hours = switch(timeframe.period)
        "1" => 1.0/60     
        "3" => 3.0/60     
        "5" => 5.0/60     
        "15" => 15.0/60   
        "30" => 30.0/60   
        "45" => 45.0/60   
        "60" => 1.0       
        "240" => 4.0      
        "D" => daily_hours  
        "W" => daily_hours * 5  
        "M" => daily_hours * 22 
        => 1.0            

    scaling_factor = math. max(current_tf_hours/daily_hours * 5,1)

    scaling_factor

ma_length = 200
lookback = 30

// Slope
tanh(x) =>
    (math.exp(2 * x) - 1) / (math.exp(2 * x) + 1)
ma200 = ta.sma(close, ma_length)
slope_func() =>
    price = ma200  
    
    start_ma = ma200[lookback]
    end_ma = ma200
    slope = (end_ma - start_ma) / lookback
    
    percentage_slope = slope / price * 100 * lookback
    
    scaling = adjust_parameter()
    scaled_percentage_slope = percentage_slope / scaling
    
    slope_indicator = 100 * tanh(scaled_percentage_slope)
    
    slope_indicator

// Convexity 
convexity_func() =>
    price = ma200

    start_ma = ma200[lookback]
    end_ma = ma200
    slope = (end_ma - start_ma) / lookback

    total_area = 0.0
    for i = 0 to lookback-1
        secant1 = start_ma + slope * (lookback - i)
        secant2 = start_ma + slope * (lookback - (i + 1))
        ma_val = ma200[lookback - i]
        h1 = secant1 - ma_val
        h2 = secant2 - ma_val
        trap_area = (h1 + h2) / 2
        total_area += trap_area

    normalized_area = (total_area / price) * lookback
    
    scaling = adjust_parameter()
    scaled_normalized_area = normalized_area / scaling
    
    convexity_indicator = 100 * tanh(scaled_normalized_area)
    
    convexity_indicator


slope_line = slope_func()
convexity_line = convexity_func()
strength_line = (slope_line + convexity_line) / 2

// Ploting
plot(slope_line, color=color.red, title="Slope Indicator", linewidth=2)
plot(ta.sma(convexity_line, 5), color=color.green, title="Convexity Indicator (5MA)", linewidth=2)
//plot(strength_line, color=color.purple, title="Strength Indicator", linewidth=2)

hline(0, color=color.white, linestyle=hline.style_solid) 

